default ch01OpeningMenuSet = set()

label ch01Opening(forcePlay = False):

    if builditTesting and (not forcePlay):
        return

    if not _in_replay:
        $ persistent.myName = "나"
        $ persistent.sunnaName = "???"
        $ persistent.wonhwaName = "???"
        $ persistent.rosalindName = "로잘린드"

    scene palace
    show 로잘린드 정면 장옷:
        pos (.5, 0) anchor (.5, .0)
        zoom .75

    play sound audio.alarmclock loop
    pause 2.5

    scene black with dissolve
    독백 "……응?"

    # 배경 – 주인공의 자취방. (밋밋한 벽지, 노트북이 올려진 책상, 커튼이 달린 창문)
    scene myRoom with dissolve

    pause 2.5

    주인공 "……아."
    stop sound
    독백 "요란한 소리와 함께 눈을 뜬 나."
    독백 "달콤한 꿈이 깨버림과 동시에 밋밋한 현실이 눈 앞에 다가왔다."
    주인공 "하아아……."
    독백 "마음만 같아선 다시 그 꿈 속으로 돌아가고 싶다."
    독백 "귓가를 때리는 휴대폰 알람을 끄며 천천히 침대에서 일어났다."
    독백 "지루하게 반복되는 일상. 나는 또다시 톱니바퀴처럼 같은 나날을 반복해야만 했다."
    주인공 "오늘은 전공 수업이었지……."
    독백 "대학생의 일과라고 해봤자 학교에서 수업 듣고 과제하는 게 전부."
    독백 "수업 끝나고 식이 녀석과 함께 운동하는 게 유일한 낙이다."
    독백 "아니지. 오늘은 일주일에 한 번 있는 치팅데이다. 몸 만들어 보겠다고 닭가슴살만 먹기도 몇 달."
    독백 "가끔 불량식품 먹기도 인생의 낙이지."
    독백 "주말에는 오전에 과제하는 시간을 빼면 종일 식이와 운동을 한다."
    독백 "자기 앞에서 치팅을 하면 먹고 싶어진대서 주말에도 철저히 식단을 지킬 수밖에 없었다."
    독백 "덕분에 일주일에 하루, 컵라면을 챙겨 가서 학교 매점에서 삼각김밥과 함께 먹는다. 역시 양념은 위대해."
    독백 "잡생각을 하며 세수를 마치고 찬장에서 컵라면 하나를 챙겼다."

    # 배경 – 자취방 입구. (철제 문으로 닫혀있다.)
    scene myRoomDoor with dissolve

    독백 "한숨이 절로 나오는 자취방 문 앞."
    독백 "오늘따라 이 문을 나서기가 유난히도 어려웠다."
    주인공 "다시 침대로 돌아가고 싶다……."
    독백 "아까 그 꿈 속 미녀랑 한 번 더 알콩달콩 노닥거릴 수만 있다면 얼마나 좋을까."
    독백 "이루어질 수 없는 소망을 속으로 바라며, 나는 문을 열었다."
    # 효과음 "덜컥, 끼이익——."
    독백 "삐걱거리는 철제 문이 열린다."
    주인공 "어!?"
    독백 "그 직후, 나는 크게 놀랄 수밖에 없었다."

    # 배경 – 자취방 입구 2(철제 문이 열려있고, 그 안에서 눈부신 빛이 쏟아져 나온다.)
    scene myRoomDoorPortal

    주인공 "뭐, 뭐야 이게!?"
    독백 "문을 열자 보이는 눈부신 빛의 무리."
    독백 "당연하게 보여야 하는 복도의 자리를 꿰찬 그 기이현상에 나는 주춤주춤 뒤로 물러났다."
    주인공 "이게 대체……?"
    독백 "인지를 넘어서는 광경. 과학적으로 도무지 설명할 방법이 없는 그것에 시선이 끌리는 것도 잠시."
    주인공 "윽, 몸이……!?"
    독백 "마치 청소기에 빨려 들어가듯, 빛의 무리에서 나오는 정체 모를 힘에 끌려가기 시작했다."
    독백 "열심히 근력을 길러왔던 나였지만, 인지를 초월한 힘 앞에서는 도저히 어찌 할 방도가 없었다."
    주인공 "우와아아악——!!"
    독백 "결국, 내 몸은 정체불명의 빛 속으로 빠져버리고 말았다."

    # 화면 전환(검은 배경으로 페이드 아웃)
    scene black with dissolve

    주인공 "으윽……."
    독백 "머리가 깨질 것 같다. 욱신거리는 이마를 손으로 감싼 채, 나는 질끈 감았던 눈을 떴다."

    # 배경 – 별천지 궁전, 궁궐 뒤뜰
    scene palaceGarden with dissolve

    주인공 "어……? 여긴……."
    독백 "아직도 생생한 아침의 꿈. 거기에 나왔던 풍경이었다."
    독백 "마치 화려한 궁전처럼 보이는 건물 내부에, 나는 홀린 듯 주위를 둘러보았다."
    독백 "이질적이면서 비현실적인 일이 벌어진 것에 대한 두려움과 호기심."
    독백 "그리고, 어쩌면 꿈 속에서처럼 그런 미녀들이 나를 반겨줄지도 모른다는, 조금 추잡한 욕심도 들었기 때문이었다."
    수나 "정말로 성공했군."
    독백 "그 찰나 들려오는 여성의 목소리."
    독백 "자연스레 그쪽으로 시선이 쏠린 내 눈 앞에 나타난 것은——."

    # 수나 스탠딩 일러스트 등장
    "(수나 꼬맹이 스탠딩)"
    주인공 "엥?"
    독백 "웬 낯선 어린아이였다."
    수나 "흠. 이번에는 또 어느 세계에서 흘러들어온 존재일까? 겉으로는 우리와 크게 다르지 않구나."
    수나 "음? 아니, 아니로군. 비슷하긴 하지만, 날개도 더듬이도 뿔도 없구나. 이거 참, 실로 요상한 생김새로다."
    주인공 "생김새라고?"
    독백 "그제야 나는 소녀의 머리에서 돋아나 있는 길쭉한 무언가를 발견할 수 있었다."
    독백 "마치 곤충의 더듬이와도 같은 그것은, 징그럽다기보단 이질적인 생김새에 더 가까웠다."
    주인공 "너, 넌 대체 누구야? 여긴 또 어디고? 설마 오늘 아침 꿈도 관련이 있는 건가?"
    수나 "오호라. 말을 하는구나. 드디어 써먹기 좋을 만한 재료를 소환했도다."
    독백 "재료라니?"
    주인공 "잠깐, 잠깐만. 너, 날 대체 어떻게 하려는 거야?"
    수나 "어떻게라니? 뭘 어떻게 한단 말이냐?"
    주인공 "나, 날 설마 잡아먹거나 그러는 건 아니지?"
    수나 "흠, 흠. 이세계인은 이상한 생각을 하는 종족이로군."
    수나 "그런 야만스러운 짓은 하지 않는도다. 그저 부려먹으려 할 따름이니라."
    주인공 "부려먹는다고?"
    독백 "설마 이 애, 나를 노예로 쓸 생각인가? 영화에서 나오는 것처럼?"
    독백 "스멀스멀 등줄기를 타고 두려움이 올라오려는 찰나, 소녀가 가슴을 쫙 펼치며 당당하게 말했다."
    수나 "자, 이세계인이여! 어서 나의 권속이 되어, 내게 이세계의 지식을 전수하거라!"
    독백 "……뭐라고?"

    # 1. 맨땅에서 시작하는 별천지 생활

    수나 "별천지에선 지식의 발전이 정체되어 있노라."
    수나 "지식에 목마른 자가 배우고자 할 바 있어도, 마침내 그 목마름을 채우지 못하는 이가 많으니라."
    수나 "내 이를 어엿비 여겨 새로운 지식을 얻고자 그대를 소환했노라."
    주인공 "별천……지?"
    수나 "아아, 그러고보니 아직 그대에게 아무것도 일러주지 않았구나. 이곳은——"
    원화 "수나 공주!"
    "(효과음 - 파지직!)"
    독백 "이건 뭐지? 순간 내 몸이 공중에 매달려 뻣뻣하게 굳었다. 동시에 눈 앞의 소녀는 어깨를 움츠리며 순식간에 사색이 되었다."
    독백 "온몸이 쇠사슬에 묶인 듯 꼼짝도 하지 못하겠다. 그저 눈알만 굴리며 사방을 둘러보았다."
    독백 "곧 우아한 자태의 여인이 한 명 나타났다."
    원화 "수나 공주! 어제도 짐이 그토록 자중하라 일렀거늘. 그새를 못 참고 또 대형사고를 치고야 말았구나!"

    $ persistent.sunnaName = "수나"

    수나 "아, 아아, 어마마마……."
    원화 "또다시 이세계의 마물을 불러들이다니! 이것은 너 자신조차 위험하게 만드는 일임을 모르겠느냐?"
    원화 "효주는 어디 있는 게냐? 공주를 보필하지 못한 죄를 물어야겠다."
    독백 "엄한 얼굴로 소녀를 꾸짖는 미모의 여성."
    독백 "여성의 시선이 수나라고 불린 아이에게서 내게로 돌아왔다."
    원화 "우선 이 마물을 내친 후, 너와 시녀들을 단단히 벌할 것이다. 각오하고 있거라."
    수나 "하오나 어마마마! 처음으로 말이 통하는 이세계의 존재이옵니다."
    원화 "말이 통한다고?"
    독백 "여인의 표정이 약간 누그러졌다."
    원화 "직접 확인해 보마."
    독백 "내 몸을 얽매고 있던 보이지 않는 힘이 조금 약해졌다."
    원화 "무엇이든 말을 해 보거라."
    주인공 "아……. 으……."
    독백 "머릿속이 완전히 엉켜서 아무런 말도 꺼내지 못했다. 나를 바라보는 여인의 표정이 한층 날카로워진다."
    원화 "괴성만 내지 않느냐? 이세계의 존재는 포악한 마물들 뿐이니라. 즉시 허공간으로 내칠 것이다!"
    수나 "아니 되옵니다, 어마마마! 이 이세계인은 소녀가 권속으로 삼을 것이옵니다."
    주인공 "야! 무슨 소리야? 누구 멋대로 나를 권속으로 삼아?"
    원화 "응?"
    독백 "너무도 어이없는 말에 나도 모르게 악을 쓰자 여인의 고개가 다시 내게로 돌아왔다."
    원화 "우리 말을 알아듣느냐?"
    주인공 "알아듣는다…… 잘 알아듣고 있습니다."
    독백 "미지의 힘을 지니고 있는 존재들이다. 게다가 오가는 말과 칭호를 듣고 있자니 왠지 사극에서 보던 왕 같다."
    독백 "혹시 모르니 우선 공손하게 대하자."
    원화 "흠……."
    독백 "여인은 공중에 결박된 나를 천천히 돌려가며 이리저리 살폈다."
    원화 "좋다. 왜 이 곳에 왔는지 말해 보라. 하지만 언사에 주의해라."
    원화 "조금이라도 이상한 낌새가 보이면 바로 내칠 것이니."
    주인공 "예. 저는 그저 학교에 가려고 집을 나섰을 뿐인데……."

    scene palaceGarden with fadeoutin

    독백 "오늘 벌어진 일을 한참 설명하자 결박이 풀렸다. 간신히 땅에 발을 대고 설 수 있었다."
    독백 "목과 어깨를 풀었다. 계속 옴짝달싹못하고 매달려 있었더니 온몸이 뻐근하다."
    원화 "미안하게 됐네. 갑작스럽게 이런 일을 겪게 만들다니."
    원화 "경솔하게 행동했다간 자칫 죄없는 자네의 목숨을 거둘 뻔 했군."
    원화 "오늘의 일은 짐이 진심으로 사과하는 바이니, 부디 너그러이 용서해주게나."
    독백 "그리 말하며 고개를 숙이는 여성과 그 광경을 경악하며 바라보는 수나."
    독백 "지금까지 오간 대화로 보아하니 이곳의 임금님인 모양인데, 그런 사람이 고개를 숙이니 그야 놀랄 만도 하겠지."

    $ persistent.wonhwaName = "원화"

    원화 "짐은 원화. 이 별천지를 다스리는 황제라네."
    주인공 "아, 네……."
    원화 "현재 상황을 소상히 파악해야겠지만, 우선은 공주와 할 이야기가 있다네."
    원화 "잠깐 응접실에서 기다리게나."

    scene palaceDrawingRoom with dissolve

    독백 "원화라고 자신을 소개한 여인이 시녀들을 불러 나를 응접실로 안내하도록 했다."
    독백 "시녀들은 차를 한 잔 내 준 후 물러났다."
    독백 "넓은 응접실에서 한참 멀뚱거리며 앉아 있자니 지루하다. 가방 정리라도 해 볼까?"
    독백 "다행히 내가 등교하려고 넣어 둔 물건들은 가방 속에 그대로 들어 있었다."
    독백 "무선 인터넷이나 전화 신호는 잡히지 않지만, 작동 자체는 잘 된다."
    독백 "물건들을 갈무리하고 잠깐 기다리자 곧 원화와 눈이 퉁퉁 부은 수나가 함께 들어왔다."
    독백 "맞은편에 앉은 폐하께서 내게 말을 건넸다."
    원화 "자네, 이름이 어떻게 되나?"
    주인공 "아, 저는……."

    if _in_replay:
        jump inputNameDone

label inputName:
    # (플레이어 이름 입력)
    $ persistent.myName = renpy.input(screen='koreaninput', default='승태', prompt='나의 이름은……')

    if len(persistent.myName) == 0:
        $ persistent.myName = "승태"

    menu(screen="namechoice"):
        "예"(noHistory=True):
            pass
        "아니요":
            jump inputName

label inputNameDone:

    원화 "[persistent.myName]라……. 독특하면서도 듣기 좋은 발음이로군. 잘 알겠네."
    원화 "그럼 다시 찬찬히 말해 주게나. 자네는 어떻게 이곳에 오게 되었나?"

    scene palaceDrawingRoom with fadeoutin

    원화 "그러니까, 자네가 살던 곳은 지구라는 별의 대한민국이라는 나라이고……."
    원화 "대학이라는 배움의 전당으로 향하려 했는데 그 순간 여기로 소환되었다는 이야기로군."
    원화 "짐이 제대로 이해하였는가?"
    주인공 "그렇습니다."
    원화 "이런이런, 쯧쯧. 이 못난 것이 자네의 일상을 완전히 뒤집어 놓았군."
    원화 "자식의 잘못은 부모의 잘못. 이것이 저지른 일은 짐이 꼭 책임질 게야."
    원화 "자네가 반드시 원래 세계로 귀환할 수 있게끔 전폭적인 지원을 약속하겠네."
    주인공 "저어, 폐하? 이렇게 부르면 됩니까?"
    원화 "왜 그러나?"
    주인공 "궁금하다고 해야 할지……. 몇 가지 질문이 있습니다."
    독백 "처음에 보이던 적대감은 완전히 사라진 듯해서 조심스레 질문을 던졌다."

    menu ch01OpeningMenu:
        set ch01OpeningMenuSet

        "여긴 대체 어디인가요?":
            원화 "이곳은 별천지. 짐이 다스리는 국가라네."
            원화 "온갖 자연의 은총과 은혜를 모두 받는, 아주 평화롭고 살기 좋은 곳이지."
            원화 "자네도 지내다 보면 이곳 생활이 꼭 마음에 들 걸세."

        "당신들은 누구인가요?":
            원화 "짐의 이름은 원화. 이 별천지를 다스리는 황제일세."
            원화 "그리고 여기 있는 못난 것은 짐의 딸, 수나 공주일세."
            수나 "들었느냐? 내가 공주임을 단단히 새겨 두어라. 앞으로 나를 공손하게 모시도록!"
            원화 "조용히 하지 못할까, 공주? 이 곳은 네 잘못에 대해 사과해야 하는 자리이니라!"
            원화 "또 다시 짐의 화를 돋운다면 그땐 처벌이 한층 무거워질 줄 알거라!"
            수나 "히잉……."

        "제가 왜 여기 있는 건가요?":
            원화 "자네는 차원소환마법에 걸려든 거라네. 짐이 분명 금지한 마법이거늘, 이 못난 아이가 그걸 발동해버리고 말았지."
            원화 "자네처럼 이성적으로 대화하는 이세계인은 짐도 처음 본다네."
            원화 "그간 봐왔던 이세계의 존재란 전부 포악하게 날뛰는 마물들 뿐이었지."
            원화 "하여, 죄없는 그들이지만, 그 목숨을 거둘 수밖에 없었다네. 제대로 돌려보낸 적이 한 번도 없었지."
            주인공 "그럼 저도 못 돌아가는 건가요?"
            원화 "숨기진 않겠네. 자네를 돌려보낼 방법은 아직 아무도 모른다네."
            주인공 "그럴 수가……."
            원화 "너무 실망하지 말게."
            원화 "내 반드시 자네가 소환된 바로 그 시간과 장소로 돌려보내도록 하겠네. 그러니 걱정 말고 이곳에서 푹 쉬게나."

    if len(ch01OpeningMenuSet) < 3:
        jump ch01OpeningMenu

    원화 "더 궁금한 사항은 없는가?"
    주인공 "으음……."
    독백 "당장 떠오르는 질문은 없다."
    원화 "질문사항이 더 없다고 생각해도 되겠나?"
    주인공 "아, 네에. 일단은요."
    원화 "그래. 언제든지 모르는 게 있다면 짐을 찾아오게나. 그럼……."

    show dragonToken:
        align (.25, .6) zoom .3
    with dissolve

    원화 "자, 받게."
    독백 "폐하께서 건넨 것은 자그마한 명패 하나."
    독백 "분위기에 휩쓸린 걸까. 나도 자연스럽게 폐하라는 명칭을 떠올리고 있었다."
    독백 "손바닥보다 조금 작은 그것을 건네주며 폐하는 부드럽게 미소지었다."

    hide dragonToken with dissolve

    원화 "자네에게 일대 후작위를 내리는 바. 그 명패를 지니고 있다면 이곳의 누구도 함부로 자네를 대하지 못할 걸세."
    수나 "어마마마, 후작이라뇨?"
    원화 "저것의 잘못을 용서받기엔 부족하겠지. 사실상 납치나 다름없는 짓을 벌이고 말았으니."
    원화 "이렇게밖에 편의를 봐줄 수 없는 짐의 무력함을 용서하게나."
    주인공 "아, 아아, 아뇨! 괜찮아요! 그러니 고개를 드세요, 폐하!"
    독백 "누군가에게 90도로 고개 숙인 사과를 받자니 민망해서 앉아 있기가 힘들었다."
    원화 "혹, 원하는 바가 있는가? 당장 가능한 일이라면 무엇이든 들어 주겠네."
    주인공 "원하는 바라……."
    독백 "그 순간 머릿속에서 아주 사악한 계획이 떠올랐다."
    독백 "수나가 나를 보자마자 했던 말, 그걸 이루어 주는 동시에 집요하게 괴롭혀 줄 수 있는 방법이었다."
    주인공 "혹시, 이 곳에도 개인 교사가 있나요?"
    원화 "물론 있지. 그건 왜 물어보는가?"
    주인공 "저를 수나 공주의 개인 교사로 삼아 주십시오. 그럼 오늘의 일은 더 이상 문제삼지 않겠습니다."
    원화 "그게 무슨 소리인가? 저것이 저지른 잘못을 그냥 스리슬쩍 넘어간다고?"
    주인공 "제가 배우던 학문이 쉽지 않습니다."
    주인공 "어려운 학문을 혹독하게 가르치다 보면 공주 본인이 질려서 저를 빨리 돌려보내려 할지도 모르니까요."
    원화 "풉……."
    원화 "생각도 못 했다네. 그런 농담도 할 줄 아는가?"
    원화 "알겠네. 자네의 뜻이 그렇다면 받아들이도록 하지."
    독백 "폐하께서 시녀들에게 몇 마디 지시를 내린 후 수나에게 고개를 돌렸다."
    원화 "수나 공주! 본래 네 마력을 봉인하고 시녀들을 모두 처벌하는 것까지 고려했다."
    원화 "하지만, 후작이 이리 말하니 지금의 네 잘못은 넘어가 주마."
    원화 "네 시녀들을 모두 석방해 두라 일렀으니 공주궁에서 대기하고 있을 게다."
    원화 "후작의 관대함이 아니었으면 죄없는 시녀들까지 네 잘못에 함께 말려들 뻔 했다. 철저히 반성하거라."
    수나 "네에, 어마마마……."
    원화 "네가 직접 후작이 지낼 만한 곳을 마련해 주어라. 부족함이 없는 곳으로 말이야."
    원화 "그리고 후작이 가르쳐 주는 내용을 모두 숙지하고 후작의 말을 잘 듣도록 하여라."
    원화 "새겨들었느냐? 이만 물러가 보거라."
    수나 "네에……."
    독백 "잔뜩 풀이 죽은 수나가 고개를 숙인 채 나를 어딘가로 이끌었다."

    # 배경 – 별천지 궁전, 궁전 복도
    scene palaceGarden with fadeoutin

    수나 "우으, 어마마마는 나만 미워해……."
    독백 "어린아이답게 칭얼거리면서 나를 어디론가 인도하는 수나."
    주인공 "네가 미움받을만한 짓만 골라서 하는 것 아냐?"
    수나 "새로운 학문에 대한 호기심이 어째서 미움 받을 만한 짓이냐?"
    독백 "수나가 갑자기 위엄 있는 말투로 살짝 화를 냈다."
    수나 "분명 이곳, 별천지는 평화롭고 분쟁도 없지."
    수나 "그 평화에 찌들어서 변화가 없노라. 고인물은 반드시 썩기 마련."
    수나 "새로운 지식을 익히고 그것을 실생활에 적용해서 백성들이 날로 살아감에 편안케 하고자 할 따름이니라."
    수나 "그런 내 노력을 쓸데없는 짓이라고 구박하다니! 이런 현실은 견딜 수 없느니라."
    독백 "이 꼬맹이 공주님, 훈민정음 서문이라도 읽었나?"
    독백 "그래도 기특한 생각이 들어서 머리를 쓰다듬어 주었다."
    주인공 "기특하다. 벌써부터 그런 생각을 하다니."
    수나 "흠… 흠……."
    독백 "눈을 감고 내 손길을 즐기는듯하던 수나가 갑자기 왈칵 화를 냈다."
    수나 "무엄하다! 감히 공주의 머리에 손을 대다니!"
    주인공 "네가 여기선 공주라도, 나한테는 그냥 꼬맹이일 뿐이야."
    수나 "꼬맹이? 이래봬도 스물이니라!"
    주인공 "뭐? 고작해야 예닐곱 살로 보이는데?"
    수나 "아아, 그대의 생태는 우리와 다른 모양이구나. 외형이 비슷해서 삶의 형태도 동일하리라 지레짐작했다."
    수나 "우리, 별천지의 사람들은 스물 두 살이 되면 우화하여 성인의 외형을 갖춘다. 그 전까지는 나와 같이 유체의 모습을 하고 있지."
    수나 "다섯 살까지 성장한 이후에는 우화할 때까지 거의 성장하지 않느니라."
    독백 "더듬이도 그렇고, 우화한다는 말도 그렇고……. 여기는 곤충 세계인가?"
    주인공 "흥미롭네. 우리는 계속해서 자라다가 어느 정도 나이가 차면 성장이 멈추는데."
    수나 "그렇구나. 서로 다른 부분을 알아가는 것도 재미 아니겠느냐?"
    독백 "이런저런 잡담을 나누며 계속 걸었다."

    수나 "곧 공주궁이구나. 내 거처이니라."
    수나 "들어가기 전에……."
    독백 "살짝 머뭇거리더니 말을 잇는 수나."
    수나 "머리를 한 번 더 쓰다듬어 다오. 은근히 좋은 기분이더구나."
    수나 "헤헤……."
    독백 "머리를 쓰다듬어 주자, 눈을 감고 가볍게 웃은 수나는 내 손을 잡고 한 건물로 이끌었다."
    수나 "내 개인교사가 되어 주어 고맙구나, [persistent.myName] 후작! 어서 이쪽으로 오너라!"
    독백 "그녀의 손에 이끌린 끝에 내가 보게 된 것은……."

    scene palaceWorkRoom
    # 로잘린드(어린아이), 말리, 효주 스탠딩 일러스트 등장
    # 루시, 꼭지, 카라 스탠딩 일러스트 등장

    show 말리 정면 한복:
        pos (.85, .0) anchor (.5, .0)
        zoom .35

    show 로잘린드 유년기:
        pos (.5, .42) anchor (.5, .0)
        zoom .35

    with dissolve


    주인공 "어?"
    독백 "아침에 꿈에서 본 미녀들이었다. 웬 꼬맹이도 한 명 끼어 있었다."

    # 로잘린드 약간 왼쪽으로 밂, 수나 유년기 스탠딩 나타남

    show 로잘린드:
        linear .5 xpos .4
    pause .5

    독백 "내가 잠깐 당황한 사이 수나는 시녀들 가운데로 쪼르르 달려가 으스댔다."
    수나 "내 시녀들이니라. 이 나라에서 고르고 골라 모은 아주 우수한 이들이로다."
    수나 "그럼, 예의에는 어긋나지만 먼저 소개하마. [persistent.myName] 후작이니라."
    수나 "내 개인교사로 특별히 이세계에서 초빙했느니라. 모두들 정성들여 모시거라."
    주인공 "{이야}[persistent.myName]{/이야}……, 입니다?"
    독백 "존대를 해야 하나?"
    수나 "어마마마께서 네게 후작위를 내리셨으니 나를 빼고는 네가 가장 높은 신분이니라. 편하게 말해도 된다."
    수나 "후작. 한 명씩 소개해 줄테니 잘 새겨 두거라."
    수나 "우선 로잘린드. 산술 능력과 기억력은 별천지 최고이지."

    show 로잘린드:
        linear .5 ypos .4 zoom .4
    pause .5

    로잘린드 "이세계의 존재입니까? 진귀한 경험이군요."
    로잘린드 "흠……."
    독백 "로잘린드라 불린 어린아이는 내 위아래를 시선으로 천천히 훑었다."
    독백 "너무 빤히 쳐다보는 시선에 약간 불쾌감이 들기도 하지만……."

    # 효과음과 홀로그램
    show ctMyself:
        pos (.15, .75) yanchor 1.
        zoom .3
    with dissolve

    독백 "로잘린드 옆에 CT 화면 같은 게 나타났다."
    로잘린드 "키는 한 길 일흔 닷 푼. 어깨가 넓고 가슴과 엉덩이가 작은 체형이군요."
    로잘린드 "태어나신 지 9,231일이 되었습니다. 근력은 53만, 마력은 0입니다."
    로잘린드 "후작님에 대한 파악이 끝났습니다. 이곳에서 편히 입으실 수 있는 옷을 짓도록 침방에 지시하겠습니다."

    show ctMyself:
        linear .5 alpha .0
    show 로잘린드:
        linear .5 ypos .42 zoom .35
    pause .5

    수나 "다음은 효주. 내 호위이자 시녀들 중에서 가장 힘이 센 장사이니라."
    효주 "반가워, 후작. 효주야. 근력이 나보다 높다니, 경쟁심 돋는데?"

    수나 "말리 소개. 생산자."
    수나 "카라 소개. 분해자."
    수나 "루시 소개. 연락 담당."
    수나 "꼭지 소개. 해충 처리반."

    수나 "로잘린드. 지금 전담 업무가 없는 시녀는 너뿐이니라."
    수나 "당분간 후작의 시중은 네가 들도록 해라. 숙소를 안내해 주거라."
    로잘린드 "알겠습니다, 공주님. 후작님? 이쪽으로 오시지요."

    scene palaceMyRoom with dissolve
    
    독백 "로잘린드를 따라가자 침대와 책상, 옷장이 있는 작은 방 하나가 나왔다."
    독백 "의외로, 세간살이는 현대 지구와 별 차이가 없다."
    로잘린드 "이 곳에서 거하시면 됩니다. 필요한 물품은 차차 장만해 드리겠습니다."
    독백 "가방을 내려놓고 침대에 걸터앉자, 로잘린드도 책상 앞에 있는 의자에 올라앉아 소매에서 쌈지를 꺼냈다."
    주인공 "로잘린드라고 했지? 그건 뭐야?"
    로잘린드 "반짇고리입니다."
    독백 "로잘린드는 입에서 실을 한 올 뱉어내더니, 바늘 두 개를 꺼내 천 비슷한 것을 짜기 시작했다."
    독백 "손놀림이 굉장히 빠르다. 그런데 이 아이, 표정 변화가 거의 없고 뜨개질만 반복해서 하고 있다."
    독백 "무슨 기계를 보는 느낌인데. 곧 짧은 반바지 같은 옷 네 벌이 완성되었다."
    로잘린드 "속옷입니다. 후작님의 체형에 맞추어 만들었습니다."
    주인공 "신기하군. 그게 마법이라는 건가?"
    로잘린드 "마법은 아니고, 제 특기 중 하나입니다. 입에서 실을 뽑을 수 있지요."
    로잘린드 "곧 저녁상과 겉옷을 올리겠으니 잠시 기다려 주십시오."

    독백 "로잘린드는 그 말까지 하고 방을 나갔다."
    독백 "침대에 드러누워 오늘 있었던 일을 하나하나 복기해 봤다."
    독백 "만화에서나 볼 법한 상황을 실제로 겪게 되다니. 분명 그 누구도 이런 이야기를 믿어주지 않겠지."
    독백 "누워서 이런저런 생각을 하다가 살살 잠이 오려는 찰나에 방문이 열렸다."

    로잘린드 "저녁상과 겉옷을 가져왔습니다."
    독백 "빈손인데?"
    독백 "로잘린드가 허공을 뒤적거리는 것처럼 손짓을 하자, 옷가지와 쟁반이 허공에서 나타났다."
    독백 "쟁반 위에는 사과처럼 생긴 열매 두 개와 물 두 잔, 나뭇잎 몇 장이 놓여 있었다. 로잘린드는 열매 하나를 내게 내밀었다."
    로잘린드 "드십시오. 사과입니다. 저희들의 주식입니다."

    독백 "겉모양은 사과지만 맛은 완전히 다르다. 씹는 느낌은 사과 그대로인데, 양념을 하지 않은 닭가슴살 맛이 옅게 난다."
    독백 "그런데 씨가 없군. 사과꼭지를 빼고는 통째로 모두 먹을 수 있었다."
    독백 "내가 사과를 먹는 동안, 로잘린드도 의자에 앉아 사과 하나를 깨끗이 먹었다."
    독백 "내게서 사과 꼭지를 받아든 로잘린드는 물잔 하나를 양손으로 들어올렸다."

    로잘린드 "차를 우려 드리겠습니다."
    독백 "잔 안에서 빛이 번쩍이더니 곧 김이 모락모락 솟아오른다."
    독백 "쟁반 위에 놓여 있던 나뭇잎이 스스로 날아올라 잔 안으로 들어갔다."
    로잘린드 "찻잎까지 모두 씹어 드십시오. 양치 효과가 있습니다."
    독백 "잔을 내게 건네 준 로잘린드는 차 한 잔을 더 우려 자신이 마셨다."

    주인공 "이번에는 마법이 맞지?"
    로잘린드 "그렇습니다. 아공간 마법과 비등 마법입니다. 후작님은 마법을 처음 보십니까?"
    주인공 "내가 살던 세계에는 마법이 없으니까."
    로잘린드 "그래서 마력이 전혀 없었군요. 저희 별천지인들은 모두 마력을 지니고 있습니다."
    로잘린드 "미약한 마력을 지닌 백성들도 물 끓이기 정도는 할 수 있지요."
    주인공 "이런 말이 어울릴지는 모르겠지만, 멋있어."
    로잘린드 "후훗……. 기분 좋은 말씀이군요."
    독백 "아주 옅은 미소를 짓는군. 감정 표현에 서투른가?"
    로잘린드 "시간이 됐군요. 저는 이만 물러나 보겠습니다."
    로잘린드 "공주님께서는 제게 후작님을 정성껏 시중들라 했습니다. 언제든 필요한 사항이 있으면 말씀 주십시오."
    로잘린드 "내일 오전부터 공주님께 개인 교육을 시켜 주시면 됩니다. 그럼……."

    독백 "로잘린드는 고개를 꾸벅 숙여 보이고는 쟁반을 들고 나갔다."
    독백 "오늘 하루, 너무 많은 일을 겪었다. 내일부터는 어떠려나?"
    독백 "뭐, 어떻게든 되겠지. 그래. 내일 일은 내일 걱정하자."
    독백 "나는 침대에 누워 그대로 잠을 청했다."

    return
