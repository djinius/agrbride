label ch05Ramen(forcePlay = False):

    if builditTesting and (not forcePlay):
        return

    # 드디어 사과에 질려 버렸다.
    # 가방에서 라면을 꺼내 먹으려는 주인공
    # 로잘린드에게 물을 끓여 달라고 함
    # 둘이서만 나눠 먹기
    # 
    # 갑자기 들이닥치는 수나와 시녀 일동
    # 라면을 죄다 빼앗기고 국물 맛만 본 주인공
    # 국수호박과 향신료, 퉁퉁마디 열매를 소개해 주는 말리
    # 어설프게 라면을 재현한 주인공
    # 텃밭 해금됨

    scene fiefMyRoom with dissolve

    독백 "보람찬 하루일을 끝마치고서 두 다리 쭈욱 펴면 영주관 내 방."
    독백 "혼잣말로 농담을 해 봐도 별로 기분이 나아지지 않는다."
    독백 "별천지에 와서 먹은 것이라곤 닭가슴살 맛 사과와 차 뿐."
    독백 "이런 음식만 먹고도 건강에 이상이 없다니……. 더 슬프다."
    독백 "아끼고 아껴 왔지만 더는 못 참겠다."
    독백 "가방을 뒤져 보았다."

    주인공 "크! 이거지!"
    독백 "나와 함께 별천지로 건너온 돗대 컵라면!"
    독백 "여기에 끓는 물만 부으면 오늘의 별식 완성이다."
    독백 "언제든 먹을 수 있지만 참는 것과 아예 먹을 수 없는 상황은 또 다르구나."
    주인공 "아아, 나의 마지막 라면이여. 오늘 나와 하나가 되자."
    독백 "그런데 끓는 물을 어디서 구하지?"

    scene fiefYardNight with dissolve

    독백 "살금살금 밖으로 나왔다."
    독백 "해가 진 이후라 조용하다. 나무 위에 루시가 매달아 둔 빛 공만이 빛나고 있었다."
    말리 몬무스 정면 "이런, 자기. 이 시간에 어디를 가?"
    주인공 "깜짝이야!"

    show 말리 몬무스 정면 놀람1:
        align (.5, .0) zoom .6
    with dissolve

    말리 "왜 그렇게 놀라?"
    주인공 "누가 있을 거라곤 생각을 못 했어."
    주인공 "너는 이 시간에 안 자고 있었어?"
    말리 보통 "밤 이슬을 마시고 있었지. 깨끗하고 순수한 물이라 맛이 좋아."
    말리 @미소1 "자기도 한 모금 마실래?"
    독백 "말리는 물이 든 조롱박을 하나 내주었다."
    주인공 "감사히 마실게."
    말리 @입벌림 "손에 든 건 뭐야?"
    주인공 "아, 아무것도 아니야."
    독백 "통이 작다고 해도 어쩔 수 없다. 이건 혼자만 먹고 싶다."
    말리 풀죽음1 "뭔가 숨기는 것 아냐, 자기?"
    주인공 "수, 숨길 리가."
    말리 보통 "흠……. 뭐 알았어. 밤에 길 잃지 않게 조심하고."
    
    hide 말리 with dissolve

    독백 "으, 땀 뺐다."

    scene fiefYardNight with fadeoutin

    독백 "한참 동안 마당을 헤매고 다녔지만 아무것도 없다."
    독백 "나뭇가지를 주워다가 비벼서 모닥불이라도 피워야 하나? 그 전에 내가 그걸 할 수는 있나?"
    독백 "아니. 불을 피운다 해도 냄비가 없다. 별천지 사람들은 마법으로 물을 끓이니 금속 식기가 필요없겠지."
    독백 "별 수 없네. 로잘린드에게 부탁해 보자."

    scene rosalindRoomNight with dissolve

    독백 "문을 살살 두들겼다."
    주인공 "로잘린드. 자?"
    로잘린드 정면 일상복1 놀람1 "주인님? 이 시간에 무슨……."

    show 로잘린드 정면 일상복1 놀람1:
        pos (.5, 0) anchor (.5, .0)
        zoom .5
    with dissolve

    로잘린드 "혹 급하신 일이라도 있으신지요?"
    주인공 "그건 아냐. 부탁이 하나 있어서……."
    로잘린드 입벌림 "이 밤중에 부탁이라뇨?"
    show 로잘린드 보통
    주인공 "혹시 물을 끓여 줄 수 있어?"
    로잘린드 "알겠습니다. 여기서 끓여 드릴까요?"
    주인공 "아니. 내 방으로 가자."

    scene fiefMyRoom
    show 로잘린드 정면 일상복1:
        pos (.5, 0) anchor (.5, .0)
        zoom .5
    with dissolve

    독백 "가방에서 나무젓가락도 꺼내고 컵라면을 뜯었다."
    로잘린드 @입벌림 "이것이 무엇입니까?"
    주인공 "지구의 음식이야. 끓는 물만 부으면 먹을 수 있지."
    로잘린드 "처음 보는 방식이군요."
    주인공 "이걸 끓여 줘."
    독백 "말리가 준 조롱박을 내밀었다. 로잘린드가 조롱박을 쓰다듬자 곧 김이 무럭무럭 오른다."
    주인공 "앗뜨뜨……."
    독백 "끓는 물을 컵라면에 붓고 뚜껑을 덮었다."
    로잘린드 "겉면에 쓰여 있는 이 글귀들이 사용법입니까?"
    주인공 "응."
    로잘린드 놀람1 "이렇게 하면 먹을 수 있는 음식이 만들어지다니, 신기합니다."
    독백 "로잘린드는 초롱초롱한 눈으로 라면을 바라보고 있었다."
    로잘린드 보통 "냄새가 상당히 자극적입니다."
    주인공 "슬슬 다 퍼졌겠다. 자, 나눠 먹자."

    # 이벤트CG - 젓가락으로 컵라면을 먹여 주려는 주인공
    독백 "컵라면을 한 젓가락 집어 로잘린드에게 내밀었다."
    로잘린드 "아……. 제가 먼저 맛을 봐도 괜찮습니까?"
    주인공 "응. 먹어봐, 먹어봐."
    로잘린드 "그럼 염치 불고하고……."
    독백 "그 순간 문이 드르륵 열렸다."

    수나 "후작! 내가 왔노라."
    주인공 "수나?"
    독백 "문 뒤에는 수나와 시녀 일행이 도열해 있었다."
    수나 "후작이 밤도깨비처럼 돌아다닌다는 말을 듣고 궁금해서 왔느니라."
    수나 "뭔가 신기하면서도 자극적인 냄새가 나는구나. 손에 들고 있는 그 물건에서 나는 것이냐?"
    독백 "숨기긴 다 틀렸구나."
    주인공 "응. 맞아. 지구의 음식이지."
    수나 "지구에서는 이런 음식을 먹는단 말이더냐? 본 공주에게도 어서 대령하거라."
    독백 "크흑. 내 마지막 컵라면이……."
    수나 "맛이 굉장히 강렬하구나. 묘한 풍미도 느껴진다."
    루시 "처음 보는 맛이야. 지구에서는 이런 음식을 먹어?"
    효주 "씹을 필요도 없이 훌훌 넘어가네."
    꼭지 "감질나."
    말리 "이런. 너무 짜. 더 먹으면 말라 비틀어지겠어."

    독백 "결국 내게 돌아온 건 국물 한 모금 뿐이었다."
    독백 "손가락으로 용기 밑바닥에 남은 국물을 삭삭 훑어서 입에 넣었다. 이 맛을 한 방울이라도 놓칠 순 없다."
    독백 "궁상이 하늘을 찌르는구나. 에휴……."
    독백 "다 먹은 용기를 버리려 할 때였다."

    카라 "후작. 그건 버리려고?"
    주인공 "응. 뭐……."
    카라 "그럼 나한테 줘."
    독백 "카라는 빈 컵라면 그릇을 깨끗이 먹어치웠다. 스티로폼 용기도 소화가 가능한가?"
    수나 "그럼 말해 보아라, 후작. 왜 이 밤중에 이걸 먹고 있었느냐?"
    독백 "다 뺏어 먹어 놓고는 왜 먹고 있었느냐고?"

    scene fiefMyRoom
    show 로잘린드 정면 일상복1:
        pos (.75, .05) anchor (.5, .0) zoom .5
    show 말리 정면 일상복1:
        pos (.25, 0) anchor (.5, .0) zoom .5
    with fadeoutin

    수나 "그러니까, 별천지의 음식이 너무 맨송맨송해서 견딜 수 없었다는 말이렷다."
    주인공 "응."
    수나 "미처 헤아리지 못했도다. 다른 세계의 사람이니 입맛도 다를 수 있음을……."
    수나 "말리의 열매는 완전식품이라 보통은 다른 음식을 먹을 필요가 없었지."
    수나 "하지만, 후작의 말을 들어 보니 가끔 별식을 준비해 주는 것도 나쁘지 않겠구나."
    수나 "꼭지, 카라. 너희들이 가장 다양한 음식을 먹으니 후작에게 무언가 추천해 보거라."
    꼭지 "오빠. 그럼 내일 나랑 장수말벌 애벌레 먹으러 갈래? 아니면 하늘소 애벌레라든지……."
    주인공 "아, 아냐. 그건 사양할게."
    카라 "똥오줌이나 쓰레기는 후작이 싫어할 것 같습니다."
    독백 "그건 호불호 이전의 문제다. 차마 입 밖으로는 꺼내지 못했지만……."
    말리 "공주님. 제가 한 말씀 올려도 되겠습니까?"
    수나 "말해 보거라, 말리."
    말리 "국수호박이라는 열매가 있습니다."
    독백 "허공에 호박 그림이 하나 떠올랐다."
    말리 "열매 속살이 결대로 풀어지는 종류입니다. 이걸 삶으면 비슷하게 만들어 볼 수 있을지도 모릅니다."
    수나 "듣던 중 반가운 말이구나. 말리. 이 자리에서 길러낼 수 있느냐?"
    말리 "분부대로……. 아까 맛을 보니 퉁퉁마디와 고추 맛도 느껴졌습니다. 함께 만들어 보겠습니다."
    독백 "말리의 손바닥에서 어린애 머리만한 호박 하나와 풀 몇 포기, 고추 몇 개가 자라났다."
    말리 "퉁퉁마디와 고추를 갈아서 넣고 끓는 물을 부으면 비슷해질 것 같습니다."
    독백 "그대로 하자, 겉으로는 라면 같은 것이 만들어졌다."
    주인공 "맛이 어떨까?"
    독백 "한 젓가락 맛을 보자, 면처럼 쫄깃한 식감이 아니고 나물처럼 아삭거리는 느낌이었다."
    독백 "국물 맛은 싱거운 콩나물국 같다. 그래도 이게 어디냐."
    수나 "그리 맛있느냐?"
    주인공 "응. 혓바닥에 스며드는 느낌이야."
    수나 "알겠도다. 후작에게 텃밭을 내어주겠노라."
    수나 "말리. 후작에게 씨앗을 만들어 주거라. 그걸 길러서 이 음식을 재현해 보거라."
    주인공 "크으. 고마워."
    수나 "맏물은 내게 가져오너라. 어마마마께 진상할 것이니라."
    수나 "그럼 이만 가 보마. 잘 자거라, 후작."
    로잘린드 "내일 아침에 뵙겠습니다, 주인님."

    독백 "결국 내 손에는 씨앗 몇 개만이 남았다. 내일부터는 텃밭을 일구게 되려나……."

    # 텃밭 해금

    return
